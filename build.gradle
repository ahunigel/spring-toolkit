plugins {
    id "io.freefair.lombok" version "8.4"
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

sourceCompatibility = 17

repositories {
    mavenCentral()
}

ext {
    springBootVersion = '3.2.0'
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    api "org.springframework.boot:spring-boot"
    implementation "org.springframework.security:spring-security-core"
    implementation "org.hibernate:hibernate-core:6.4.1.Final"

    api "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final"
    implementation 'com.google.guava:guava:32.1.3-jre'

    testImplementation group: 'junit', name: 'junit'
    api 'net.ttddyy:datasource-proxy:1.10'
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'net.bytebuddy:byte-buddy' // allows mocking of classes (in addition to interfaces)
    testImplementation "org.objenesis:objenesis:3.3"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ahunigel/${project.name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            
            pom {
                name = project.name
                description = 'spring toolkit'
                url = "https://github.com/ahunigel/${project.name}"
                developers {
                    developer {
                        id = 'ahunigel'
                        name = 'Nigel Zheng'
                        email = 'ahunigel@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:ahunigel/${project.name}.git"
                    url = "https://github.com/ahunigel/${project.name}"
                }
            }
        }
    }
}
