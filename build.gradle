plugins {
    id "io.freefair.lombok" version "9.2.0"
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

ext {
    springBootVersion = '4.0.2'
    groovyVersion = '4.0.24'
}

configurations.all {
    resolutionStrategy {
        force "org.apache.groovy:groovy:${groovyVersion}"
        force "org.apache.groovy:groovy-json:${groovyVersion}"
        force "org.apache.groovy:groovy-nio:${groovyVersion}"
        force "org.apache.groovy:groovy-sql:${groovyVersion}"
        force "org.apache.groovy:groovy-xml:${groovyVersion}"
        force "org.apache.groovy:groovy-templates:${groovyVersion}"
    }
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    implementation "org.springframework.boot:spring-boot"
    implementation "org.springframework.security:spring-security-core"
    implementation "org.hibernate:hibernate-core:7.2.3.Final"

    implementation "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final"
    implementation 'com.google.guava:guava:33.5.0-jre'

    testImplementation group: 'junit', name: 'junit'
    implementation 'net.ttddyy:datasource-proxy:1.11.0'
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testImplementation platform("org.spockframework:spock-bom:2.4-groovy-4.0")
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'net.bytebuddy:byte-buddy' // allows mocking of classes (in addition to interfaces)
    testImplementation "org.objenesis:objenesis:3.5"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}
// Make all tests use JUnit 5
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ahunigel/${project.name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            
            pom {
                name = project.name
                description = 'spring toolkit'
                url = "https://github.com/ahunigel/${project.name}"
                developers {
                    developer {
                        id = 'ahunigel'
                        name = 'Nigel Zheng'
                        email = 'ahunigel@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:ahunigel/${project.name}.git"
                    url = "https://github.com/ahunigel/${project.name}"
                }
            }
        }
    }
}
